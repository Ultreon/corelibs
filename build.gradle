apply plugin: 'maven-publish'

group 'io.github.ultreon.corelibs'
version '0.1.1'

mkdir("${rootProject.projectDir}/.mvnrepo")

subprojects {
    apply plugin: 'java'
    apply plugin: "java-library"
    apply plugin: 'maven-publish'

    group 'io.github.ultreon.corelibs'

    archivesBaseName = "corelibs-$project.name"

    def admin = rootProject.hasProperty('sonatypeUsername')

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"

        implementation "org.jetbrains:annotations:$annotations_version"
    }

    processResources {
        from file("$rootProject.projectDir/LICENSE.md")
    }

    test {
        useJUnitPlatform()
    }

    java {
        withSourcesJar()
    }

    compileJava {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }

    jar.archiveBaseName = "corelibs-$project.name"
    sourcesJar.archiveBaseName = "corelibs-$project.name"

    publishing {
        publications {
            register('mavenJava', MavenPublication) {
                artifact jar
            }
        }
        repositories {
            maven {
                url "file://${rootProject.projectDir}/.mvnrepo"
            }
        }
    }

    afterEvaluate {
        publish {
            dependsOn "build"
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
