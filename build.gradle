apply plugin: 'maven-publish'

group 'tk.ultreonteam.corelibs'
version '0.1.0'

subprojects {
    apply plugin: 'java'
    apply plugin: "java-library"
    apply plugin: 'maven-publish'

    group 'tk.ultreonteam.corelibs'

    archivesBaseName = "corelibs-$project.name"

    ext.admin = hasProperty('sonatypeUsername')

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"

        implementation "org.jetbrains:annotations:$annotations_version"
    }

    processResources {
        from file("$rootProject.projectDir/LICENSE.md")
    }

    test {
        useJUnitPlatform()
    }

    java {
        withSourcesJar()
    }

    jar.archiveBaseName = "corelibs-$project.name"
    sourcesJar.archiveBaseName = "corelibs-$project.name"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = "corelibs-$project.name"
            }
        }
        repositories {
            maven {
                name "Sonatype"
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                if (admin) {
                    credentials {
                        username property("sonatypeUsername").toString()
                        password property("sonatypePassword").toString()
                    }
                }
            }
        }
    }
}
